dnl $Id$

dnl Version information
m4_define([xfburn_version_major], [0])
m4_define([xfburn_version_minor], [3])
m4_define([xfburn_version_micro], [3])
m4_define([xfburn_version_build], [@REVISION@])
m4_define([xfburn_version_tag], [svn])
m4_define([xfburn_version], [xfburn_version_major().xfburn_version_minor().xfburn_version_micro()ifelse(xfburn_version_tag(), [svn], [xfburn_version_tag()-xfburn_version_build()], [xfburn_version_tag()])])

dnl Initialize autoconf
AC_COPYRIGHT([Copyright (c) 2005-2008 Jean-Fran√ßois Wauthy <pollux@xfce.org>, David Mohr <david@mcbf.net>])
AC_INIT([xfburn], [xfburn_version], [xfburn@xfce.org])

dnl Initialize automake
AM_INIT_AUTOMAKE([AC_PACKAGE_TARNAME()], [AC_PACKAGE_VERSION()])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()

dnl check for basic programs
AC_PROG_CC()
AC_PROG_INSTALL()
AC_PROG_LIBTOOL()
AC_PROG_INTLTOOL([0.31], [no-xml])

AC_SYS_LARGEFILE

dnl check for i18n support
XDT_I18N([@LINGUAS@])

dnl check for required packages
XDT_CHECK_PACKAGE([LIBBURN], [libburn-1], [0.4.2])
XDT_CHECK_PACKAGE([LIBISOFS], [libisofs-1], [0.6.2])
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.6.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.10.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.4.0])
XDT_CHECK_PACKAGE([EXO], [exo-0.3], [0.3.0])
XDT_CHECK_OPTIONAL_PACKAGE([THUNAR_VFS], [thunar-vfs-1], [0.3.0], [thunar-vfs],
	AC_HELP_STRING([--disable-thunar-vfs],
			[Disable the use of Thunar's VFS layer to show mime type and icons in file browser]))
AM_CONDITIONAL([HAVE_THUNAR_VFS], [test "x$THUNAR_VFS_FOUND" = "xyes"])

dnl **********************************
dnl *** Optional support for D-BUS ***
dnl **********************************
dnl (taken from thunar-0.8.0)
XDT_CHECK_OPTIONAL_PACKAGE([DBUS], [dbus-glib-1],
                           [0.34], [dbus], [D-BUS support])

dnl ********************************
dnl *** Check for HAL (optional) ***
dnl ********************************
dnl (taken from libexo's configure.in.in rev.26877)
XDT_CHECK_OPTIONAL_PACKAGE([HAL], [hal-storage], [0.5.7], [hal], [HAL support], [yes])
if test x"$HAL_FOUND" = x"yes"; then
  dnl # substituted into exo-hal-0.3.pc
  HAL_STORAGE_DEPENDENCY="hal-storage"

  dnl # Check if LIBHAL_DRIVE_CDROM_CAPS_DVDPLUSRWDL is declared
  save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS $HAL_CFLAGS"
  AC_MSG_CHECKING([whether LIBHAL_DRIVE_CDROM_CAPS_DVDPLUSRWDL is declared])
  AC_COMPILE_IFELSE(AC_LANG_SOURCE(
  [#include <libhal-storage.h>
    int main (int argc, char **argv) { return LIBHAL_DRIVE_CDROM_CAPS_DVDPLUSRWDL; }
  ]),
  [
    AC_DEFINE([HAVE_LIBHAL_DRIVE_CDROM_CAPS_DVDPLUSRWDL], [1], [Define if LIBHAL_DRIVE_CDROM_CAPS_DVDPLUSRWDL present])
    AC_MSG_RESULT([yes])
  ],
  [
    AC_MSG_RESULT([no])
  ])
  CFLAGS="$save_CFLAGS"
fi
AC_SUBST([HAL_STORAGE_DEPENDENCY])


dnl check for debugging support
BM_DEBUG_SUPPORT()

AC_OUTPUT([
Makefile
icons/Makefile
icons/16x16/Makefile
icons/22x22/Makefile
icons/24x24/Makefile
icons/32x32/Makefile
icons/48x48/Makefile
icons/scalable/Makefile
po/Makefile.in
xfburn/Makefile
])
	
